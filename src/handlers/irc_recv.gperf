%{
#define RECV_HANDLERS \
	X(error) \
	X(join) \
	X(kick) \
	X(mode) \
	X(nick) \
	X(notice) \
	X(part) \
	X(ping) \
	X(pong) \
	X(privmsg) \
	X(quit) \
	X(topic)

#define X(cmd) static int recv_##cmd(struct parsed_mesg*, struct server*);
RECV_HANDLERS
#undef X

struct recv_handler
{
	char *name;
	int (*func)(struct parsed_mesg*, struct server*);
};
%}

%enum
%null-strings
%readonly-tables
%struct-type
%define word-array-name      recv_handlers
%define hash-function-name   recv_handler_hash
%define lookup-function-name recv_handler_lookup
struct recv_handler;
%%
ERROR,   recv_error
JOIN,    recv_join
KICK,    recv_kick
MODE,    recv_mode
NICK,    recv_nick
NOTICE,  recv_notice
PART,    recv_part
PING,    recv_ping
PONG,    recv_pong
PRIVMSG, recv_privmsg
QUIT,    recv_quit
TOPIC,   recv_topic
