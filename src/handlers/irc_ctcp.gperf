%{
#include <string.h>

#define CTCP_HANDLERS \
	X(action) \
	X(clientinfo) \
	X(ping) \
	X(source) \
	X(time) \
	X(version)

#define X(cmd) static int recv_ctcp_request_##cmd(struct server*, struct parsed_mesg*);
CTCP_HANDLERS
#undef X

#define X(cmd) static int recv_ctcp_response_##cmd(struct server*, struct parsed_mesg*);
CTCP_HANDLERS
#undef X

typedef int (*ctcp_f)(struct server*, struct parsed_mesg*);

struct ctcp_handler
{
	const char *key;
	ctcp_f f_request;
	ctcp_f f_response;
};
%}

%enum
%null-strings
%readonly-tables
%struct-type
%define slot-name key
%define word-array-name      ctcp_handlers
%define hash-function-name   ctcp_handler_hash
%define lookup-function-name ctcp_handler_lookup
%define initializer-suffix ,(ctcp_f)0,(ctcp_f)0
struct ctcp_handler;
%%
ACTION,     recv_ctcp_request_action,     recv_ctcp_response_action
CLIENTINFO, recv_ctcp_request_clientinfo, recv_ctcp_response_clientinfo
PING,       recv_ctcp_request_ping,       recv_ctcp_response_ping
SOURCE,     recv_ctcp_request_source,     recv_ctcp_response_source
TIME,       recv_ctcp_request_time,       recv_ctcp_response_time
VERSION,    recv_ctcp_request_version,    recv_ctcp_response_version
